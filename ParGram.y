-- -*- haskell -*- File generated by the BNF Converter (bnfc 2.9.5).

-- Parser definition for use with Happy
{
{-# OPTIONS_GHC -fno-warn-incomplete-patterns -fno-warn-overlapping-patterns #-}
{-# LANGUAGE PatternSynonyms #-}

module ParGram where

import Prelude

import qualified TypeSystem as TS
import qualified Env as E
import qualified ErrS as Err
import qualified AbsGram as Abs 
import LexGram

}

%name pProgram Program
%name pListStm ListStm
%name pBasicType BasicType
%name pBoolean Boolean
%name pStm Stm
%name pDecl Decl
%name pListParam ListParam
%name pParam Param
%name pRExp RExp
%name pRExp2 RExp2
%name pRExp3 RExp3
%name pRExp4 RExp4
%name pListRExp ListRExp
%name pRExp1 RExp1
-- no lexer declaration
%monad { Err } { (>>=) } { return }
%tokentype {Token}
%token
  '!'        { PT _ (TS _ 1)  }
  '!='       { PT _ (TS _ 2)  }
  '%'        { PT _ (TS _ 3)  }
  '&&'       { PT _ (TS _ 4)  }
  '('        { PT _ (TS _ 5)  }
  ')'        { PT _ (TS _ 6)  }
  '*'        { PT _ (TS _ 7)  }
  '+'        { PT _ (TS _ 8)  }
  ','        { PT _ (TS _ 9)  }
  '-'        { PT _ (TS _ 10) }
  '/'        { PT _ (TS _ 11) }
  ';'        { PT _ (TS _ 12) }
  '<'        { PT _ (TS _ 13) }
  '<='       { PT _ (TS _ 14) }
  '='        { PT _ (TS _ 15) }
  '=='       { PT _ (TS _ 16) }
  '>'        { PT _ (TS _ 17) }
  '>='       { PT _ (TS _ 18) }
  'False'    { PT _ (TS _ 19) }
  'String'   { PT _ (TS _ 20) }
  'True'     { PT _ (TS _ 21) }
  '['        { PT _ (TS _ 22) }
  ']'        { PT _ (TS _ 23) }
  'bool'     { PT _ (TS _ 24) }
  'break'    { PT _ (TS _ 25) }
  'char'     { PT _ (TS _ 26) }
  'continue' { PT _ (TS _ 27) }
  'do'       { PT _ (TS _ 28) }
  'else'     { PT _ (TS _ 29) }
  'float'    { PT _ (TS _ 30) }
  'if'       { PT _ (TS _ 31) }
  'int'      { PT _ (TS _ 32) }
  'return'   { PT _ (TS _ 33) }
  'while'    { PT _ (TS _ 34) }
  '{'        { PT _ (TS _ 35) }
  '||'       { PT _ (TS _ 36) }
  '}'        { PT _ (TS _ 37) }
  L_Ident    { PT _ (TV $$)   }
  L_charac   { PT _ (TC $$)   }
  L_doubl    { PT _ (TD $$)   }
  L_integ    { PT _ (TI $$)   }
  L_quoted   { PT _ (TL $$)   }

%attributetype {Attr a}
%attribute res { Result }
%attribute attr { a }
%attribute err { [String] }
%attribute env { E.EnvT }
%attribute modifiedEnv { E.EnvT }
%attribute ident { String }
%attribute pos { Posn }
%attribute btype { TS.Type }
%attribute funcName { String }
%attribute paramTypes { [TS.Type] }

%%

Ident  : L_Ident 
  { 
    $$.attr = Abs.Ident $1;
    $$.ident = $1;
    $$.err = ["--IDENT--"];
    $$.pos = $1;
  }

Double   : L_doubl  
  { 
    $$.attr = (read $1) :: Double;
    $$.err = [];
    $$.btype = (TS.Base TS.FLOAT);
  }

Integer  : L_integ  
  { 
    $$.attr = (read $1) :: Integer;
    $$.err = [];
    $$.btype = (TS.Base TS.INT);
  }

Char    : L_charac
  { 
    $$.attr =  (read $1) :: Char;
    $$.err = [];
    $$.btype = (TS.Base TS.CHAR);
  }

String   : L_quoted 
  {
    $$.attr =  $1;
    $$.err = [];
    $$.btype = (TS.Base TS.STRING);
  }

Boolean: 'True' 
  { 
    $$.attr = Abs.Boolean_True;
    $$.err = [];
    $$.btype = (TS.Base TS.BOOL);
  }
  | 'False' 
  { 
    $$.attr = Abs.Boolean_False;
    $$.err = [];
    $$.btype = (TS.Base TS.BOOL);
  }

  BasicType: 'int' 
  { 
    $$.attr = Abs.BasicType_int;
    $$.btype = TS.Base TS.INT;
  }
  | 'float'   
  { 
    $$.attr = Abs.BasicType_float;
    $$.btype = TS.Base TS.FLOAT;
  }
  | 'char'   
  { 
    $$.attr = Abs.BasicType_char;
    $$.btype = TS.Base TS.CHAR;
  }
  | 'String'   
  { 
    $$.attr = Abs.BasicType_String;
    $$.btype = TS.Base TS.STRING;
  }
  | 'bool'  
  { 
    $$.attr = Abs.BasicType_bool;
    $$.btype = TS.Base TS.BOOL;
  }

Program : ListStm 
  { 
    $$.res = Result (Abs.ProgramStart $1.attr) $1.err;
    $1.env = E.emptyEnv;
  }

ListStm : Stm ';' 
  { 
    $$.attr = (:[]) $1.attr;
    $1.env = $$.env;
    $$.err = $1.err;
  } 
  | Stm ';' ListStm 
  { 
    $$.attr = (:) $1.attr $3.attr;
    $1.env = $$.env;
    $3.env = $1.modifiedEnv;
    $$.err = $1.err ++ $3.err;
  }

Stm: Decl
  { 
    $$.attr = Abs.Declaration $1.attr;
    $1.env = $$.env; 
    $$.modifiedEnv = $1.modifiedEnv;
    $$.err = $1.err;
    $$.ident = $1.ident;
    $$.btype = $1.btype;
  }
  | 'if' '(' RExp ')' '{' ListStm '}'
  { 
    $$.attr = Abs.IfThen $3.attr $6.attr;
    $3.env = $$.env;
    $6.env = $$.env;
    $$.modifiedEnv = $$.env;
    $$.err = Err.mkIfErrs $3.btype $6.err (posLineCol (tokenPosn $1));
  }
  | 'if' '(' RExp ')' '{' ListStm '}' 'else' '{' ListStm '}' 
  { 
    $$.attr = Abs.IfThenElse $3.attr $6.attr $10.attr;
    $3.env = $$.env;
    $6.env = $$.env;
    $10.env = $$.env;
    $$.modifiedEnv = $$.env;
    $$.err = Err.mkIfErrs $3.btype ($6.err ++ $10.err) (posLineCol (tokenPosn $1));
  }
  | 'while' '(' RExp ')' '{' ListStm '}' 
  { 
    $$.attr = Abs.WhileDo $3.attr $6.attr; 
    $3.env = $$.env;
    $6.env = E.insertVar "continue" (posLineCol (tokenPosn $1)) (TS.Base TS.BOOL) (E.insertVar("break") (posLineCol (tokenPosn $1)) (TS.Base TS.BOOL) $$.env);
    $$.modifiedEnv = $$.env;
    $$.err = Err.mkIfErrs $3.btype $6.err (posLineCol (tokenPosn $1));
  }
  | 'do' '{' ListStm '}' 'while' '(' RExp ')' 
  { 
    $$.attr = Abs.DoWhile $3.attr $7.attr;
    $3.env = E.insertVar "continue" (posLineCol (tokenPosn $1)) (TS.Base TS.BOOL) (E.insertVar("break") (posLineCol (tokenPosn $1)) (TS.Base TS.BOOL) $$.env);	
    $7.env = $$.env;
    $$.modifiedEnv = $$.env;
    $$.err = Err.mkIfErrs $7.btype $3.err (posLineCol (tokenPosn $1)); 
  }
  | 'break' 
  { 
    $$.attr = Abs.Break;
    $$.modifiedEnv = $$.env;
    $$.err = if E.containsEntry "break" $$.env
            then []
            else [Err.mkSerr (TS.Base (TS.ERROR "Break statement outside of loop")) (posLineCol $$.pos)];
    $$.pos = (tokenPosn $1);
  }
  | 'continue' 
  { 
    $$.attr = Abs.Continue;
    $$.modifiedEnv = $$.env;
    $$.err = if E.containsEntry "continue" $$.env
            then []
            else [Err.mkSerr (TS.Base (TS.ERROR "Continue statement outside of loop")) (posLineCol $$.pos)];
    $$.pos = (tokenPosn $1);
  }
  | 'return' RExp 
  { 
    $$.attr = Abs.Return $2.attr;
    $$.btype = $2.btype;
    $2.env = $$.env;
    $$.modifiedEnv = $$.env;

    $$.err = (Err.mkReturnErrs $$.env $2.btype ( posLineCol $$.pos)) ++ $2.err;

    $$.pos = (tokenPosn $1);
  }
  | Ident '=' RExp 
  {  
    $$.attr = Abs.Assignment $1.attr $3.attr;
    $$.modifiedEnv = E.insertVar $1.ident (posLineCol $$.pos) $$.btype $$.env;
    $$.err = Err.mkAssignmentErrs (E.getVarType $1.ident $$.env) $3.btype (posLineCol $$.pos) ++ $3.err;
    $$.ident = $1.ident;
    $$.pos = $1.pos;
    $$.btype = TS.sup (E.getVarType $1.ident $$.env) (E.getVarType $3.ident $$.env);
    $3.env = $$.env;
   }

Decl: BasicType Ident '=' RExp 
  {   
    $$.attr = Abs.VarDeclaration $1.attr $2.attr $4.attr;
    $$.modifiedEnv = E.insertVar $2.ident (posLineCol $$.pos) $$.btype $$.env;
    $$.err = Err.mkDeclErrs $1.btype $4.btype $$.env $2.ident (posLineCol $$.pos) ++ $4.err; 
    $$.ident = $2.ident;
    $$.pos = $2.pos;
    $$.btype = TS.sup $4.btype $1.btype;
    $4.env = $$.env;
  }
  | BasicType Ident '[' RExp ']' 
  { 
    $$.attr = Abs.ArrayDeclaration $1.attr $2.attr $4.attr;
    $$.modifiedEnv = E.insertVar $2.ident (posLineCol $$.pos) $$.btype $$.env;
    $$.err = Err.mkArrayDeclErrs $4.btype $$.env $2.ident (posLineCol $$.pos) ++ $4.err;
    $$.ident = $2.ident;
    $$.pos = $2.pos;
    $$.btype = (TS.ARRAY $4.btype $1.btype);  
  }
  | BasicType Ident '(' ListParam ')' '{' ListStm '}' 
  { 
    $$.attr = Abs.FunctionDeclaration $1.attr $2.attr $4.attr $7.attr; 

    $$.modifiedEnv = E.insertFunc $2.ident (posLineCol $$.pos) $1.btype $4.paramTypes $$.env;
    $7.env = $4.modifiedEnv;
    $4.env = E.insertVar "return" (posLineCol ($2.pos)) ($$.btype) E.emptyEnv;

    $4.funcName = $2.ident;
    $$.btype = $1.btype;

    $$.err = $4.err ++ (Err.mkFuncErrs $7.err $2.ident);
  }

ListParam: {- empty -} 
  { 
    $$.attr = []; 
    $$.modifiedEnv = $$.env;
    $$.err = [];

    $$.paramTypes = [];
  }
  | Param 
  { 
    $$.attr = (:[]) $1.attr; 
    $1.env = $$.env;
    $$.modifiedEnv = $1.modifiedEnv;
    $1.funcName = $$.funcName;
    $$.err = $1.err;

    $$.paramTypes = [$1.btype];
  }
  | Param ',' ListParam 
  { 
    $$.attr = (:) $1.attr $3.attr; 

    $1.env = $$.env;
    $3.env = $1.modifiedEnv;
    $$.modifiedEnv = $3.modifiedEnv;
    $1.funcName = $$.funcName;
    $3.funcName = $$.funcName;

    $$.err = $1.err ++ $3.err;

    $$.paramTypes = $1.btype : $3.paramTypes;
  }

Param : BasicType Ident 
  { 
    $$.attr = Abs.Parameter $1.attr $2.attr; 

    $$.modifiedEnv = E.insertVar $2.ident (posLineCol $$.pos) $1.btype $$.env;

    $$.pos = $2.pos;

    $$.err = Err.mkParamErrs $2.ident $$.funcName $$.env (posLineCol $$.pos);

    $$.btype = $1.btype;
  }

RExp: RExp '||' RExp2 
  {
    $$.attr = Abs.Or $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.isBoolean (TS.sup $1.btype $3.btype); 
    $1.env = $$.env;  
    $3.env = $$.env;
  }
  | RExp '&&' RExp2 
  {
    $$.attr = Abs.And $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.isBoolean (TS.sup $1.btype $3.btype); 
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | '!' RExp2 
  { 
    $$.attr = Abs.Not $2.attr;
    $$.err = $2.err;
    $$.btype = TS.isBoolean $2.btype; 
    $2.env = $$.env;
  }
  | RExp1 
  { 
    $$.attr = $1.attr; 
    $$.err = $1.err;
    $$.btype = $1.btype;
    $1.env = $$.env;
  }

RExp2: RExp2 '==' RExp3 
  { 
    $$.attr = Abs.Eq $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.rel $1.btype $3.btype; 
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | RExp2 '!=' RExp3 
  { 
    $$.attr = Abs.Neq $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.rel $1.btype $3.btype; 
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | RExp2 '<' RExp3 
  { 
    $$.attr = Abs.Lt $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.rel $1.btype $3.btype; 
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | RExp2 '>' RExp3 
  { 
    $$.attr = Abs.Gt $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.rel $1.btype $3.btype; 
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | RExp2 '<=' RExp3 
  { 
    $$.attr = Abs.Le $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.rel $1.btype $3.btype; 
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | RExp2 '>=' RExp3 
  { 
    $$.attr = Abs.Ge $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.rel $1.btype $3.btype; 
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | RExp3 
  {
    $$.attr = $1.attr; 
    $$.err = $1.err;
    $$.btype = $1.btype;
    $1.env = $$.env;
  }

RExp3: RExp3 '+' RExp4 
  { 
    $$.attr = Abs.Add $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.sup $1.btype $3.btype;
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | RExp3 '-' RExp4 
  { 
    $$.attr = Abs.Sub $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.sup $1.btype $3.btype;
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | RExp3 '*' RExp4 
  { 
    $$.attr = Abs.Mul $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.sup $1.btype $3.btype;
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | RExp3 '/' RExp4 
  { 
    $$.attr = Abs.Div $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.sup $1.btype $3.btype;
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | RExp3 '%' RExp4 
  { 
    $$.attr = Abs.Mod $1.attr $3.attr;
    $$.err = $1.err ++ $3.err;
    $$.btype = TS.sup $1.btype $3.btype;
    $1.env = $$.env;
    $3.env = $$.env;
  }
  | RExp4 
  {     
    $$.attr = $1.attr; 
    $$.err = $1.err;
    $$.btype = $1.btype;
    $1.env = $$.env;
  }

RExp4: Integer 
  { 
    $$.attr = Abs.IntValue $1.attr; 
    $$.err = $1.err;
    $$.btype = $1.btype;
  }
  | Double  
  { 
    $$.attr = Abs.FloatValue $1.attr;
    $$.err = $1.err;
    $$.btype = $1.btype;
  }
  | String  
  { 
    $$.attr = Abs.StringValue $1.attr;
    $$.err = $1.err;
    $$.btype = $1.btype;
  }
  | Char  
  { 
    $$.attr = Abs.CharValue $1.attr;
    $$.err = $1.err;
    $$.btype = $1.btype;
  }
  | Boolean  
  { 
    $$.attr = Abs.BooleanValue $1.attr;
    $$.err = $1.err;
    $$.btype = $1.btype;
  }
  | Ident  
  { 
    $$.attr = Abs.VarValue $1.attr;
    $$.err = if E.containsEntry $1.ident $$.env
            then []
            else [Err.mkSerr (TS.Base (TS.ERROR ("Variable " ++ $1.ident ++ " not declared"))) (posLineCol $1.pos)];
    $$.btype = (E.getVarType $1.ident $$.env);
  }
  
  | Ident '(' ListRExp ')' 
  { 
    $$.attr = Abs.FuncCall $1.attr $3.attr;
    $3.env = $$.env;

    $$.btype = (E.getFuncType $1.ident $$.env);
    $$.err = (Err.mkFuncCallErrs $1.ident $3.paramTypes $$.env (posLineCol $1.pos)) ++ $3.err;
  }
  | '(' RExp ')'  
  { 
    $$.attr =  $2.attr;
    $$.err = $2.err;
    $$.btype = $2.btype;
    $2.env = $$.env;
  }

ListRExp: {- empty -} 
  { 
    $$.attr = [];

    $$.err = [];
    $$.paramTypes = [];
  }
  | RExp 
  { 
    $$.attr = (:[]) $1.attr; 
    $1.env = $$.env;

    $$.err = $1.err;
    $$.paramTypes = [$1.btype];
  }
  | RExp ',' ListRExp 
  { 
    $$.attr = (:) $1.attr $3.attr; 
    $1.env = $$.env;
    $2.env = $$.env;

    $$.err = $1.err ++ $3.err;
    $$.paramTypes = $1.btype : $3.paramTypes;
  }

RExp1 : RExp2 
{
    $$.attr = $1.attr;
    $$.err = $1.err;
    $$.btype = $1.btype;
    $1.env = $$.env;
}

{

data Result = Result Abs.Program [String] deriving (Show)

type Err = Either String

happyError :: [Token] -> Err a
happyError ts = Left $
  "syntax error at " ++ tokenPos ts ++
  case ts of
    []      -> []
    [Err _] -> " due to lexer error"
    t:_     -> " before `" ++ (prToken t) ++ "'"

myLexer :: String -> [Token]
myLexer = tokens

}